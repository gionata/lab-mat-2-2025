{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c6c809ea-fe39-442b-91c1-5d82d2979320",
   "metadata": {},
   "source": [
    "# Laboratorio di Matematica 2: Giochi di numeri\n",
    "\n",
    "Il corso mira a sviluppare il pensiero ricorsivo attraverso lo sviluppo dell'aritmetica.\n",
    "Tra induzione e ricorsione saranno definiti l'insieme dei numeri naturali e, con le sole funzioni primitive di riconoscimento dello `zero`, `successore` e `predecessore`, saranno definiti alcuni operatori di confronto e operazioni di *addizione*, *sottrazione*, *moltiplicazione* e *divisione intera*.\n",
    "Lo stesso modo di ragionare su problemi numerici può essere applicato alla risoluzione di tutti problemi che l'informatica può risolvere.\n",
    "Sono questi ragionamenti il vero oggetto del corso.\n",
    "\n",
    "Gli obiettivi generali del corso includono:\n",
    "\n",
    "- Comprendere meno superficialmente gli oggetti di studio e i metodi della disciplina informatica per poter orientare con maggior consapevolezza le proprie scelte future.\n",
    "- Stimolare l’interesse verso le materie tecnico scientifiche e, in particolare, verso l’informatica.\n",
    "- Cogliere la stretta relazione tra pensiero scientifico e sviluppo tecnologico.\n",
    "- Comprendere le strutture fondamentali dei ragionamenti logico-deduttivi e padroneggiare il linguaggio logico-formale per risolvere problemi di varia natura.\n",
    "- Utilizzare strumenti informatici per modellizzare e risolvere problemi.\n",
    "\n",
    "## Un linguaggio per definire formalmente le idee\n",
    "\n",
    "Useremo un linguaggio chiamato *Scheme* per descrivere le idee sulle espressioni e sulle operazioni tra numeri naturali\n",
    "\n",
    "Il corso sarà basato su alcuni dei primi capitoli di \"The Little Schemer\", un testo del 1974 che introduce il concetto di ricorsione.\n",
    "Il prosieguo di questo *notebook* sviluppa le definizioni come proposte dal testo, con riferimento alla quarta edizione.\n",
    "Per facilitare la comprensione si sono tradotti quasi tutti i termini usati in Scheme - e in Lisp - come da tabella.\n",
    "\n",
    "|Scheme|Questo notebook|\n",
    "|------|---------------|\n",
    "| car  | primo         |\n",
    "| cons | anteponi      |\n",
    "| cdr  | resto         |\n",
    "| list | lista         |\n",
    "| list?| lista?        |\n",
    "| null?| lista-vuota?  |\n",
    "| '()  | lista-vuota   |\n",
    "| eq?  | uguale?       |\n",
    "\n",
    "Questi nomi servono a definire un modo di organizzare di dati, quello della lista:\n",
    "\n",
    "```\n",
    "\n",
    "  primo    resto         primo    resto  lista-vuota\n",
    ".-------.--------.     .-------.--------.     |\n",
    "|       |    *---|---->|       |    *---|---->|\n",
    "|_______|________|     |_______|________|     |\n",
    "\n",
    "```\n",
    "\n",
    "Un esempio di lista è\n",
    "\n",
    "`'(primo secondo contorno)`\n",
    "\n",
    "```\n",
    "\n",
    "  primo    resto         primo    resto           primo      resto   lista-vuota\n",
    ".-------.--------.     .---------.--------.     .----------.--------.     |\n",
    "| primo |    *---|---->| secondo |    *---|---->| contorno |    *---|---->|\n",
    "|_______|________|     |_________|________|     |__________|________|     |\n",
    "\n",
    "```\n",
    "\n",
    "\n",
    "Per formare delle procedure useremo una strana sintassi: \n",
    "\n",
    "```\n",
    "(lambda (parametro parametro...)\n",
    "    (definizione))\n",
    "```\n",
    "\n",
    "La `lambda`, ossia la lettera $\\lambda$, deriva da un sistema formale chiamato $\\lambda$-calcolo. \n",
    "\n",
    "Le liste le possiamo usare per chiamare le procedure e useremo il primo elemento per denotare la procedura, gli altri per gli argomenti.\n",
    "\n",
    "```\n",
    "(+ 1 2)\n",
    "```\n",
    "\n",
    "Indica l'applicazione dell'operatore `+` ai numeri `1` e `2`. In matematica lo scriveremo come `1 + 2`. La valutazione produce il valore `3`.\n",
    "\n",
    "```\n",
    "     .                 .-------.   \n",
    "   / | \\               |   +   |\n",
    "  /  |  \\              |-------|\n",
    " /   |   \\             | 1 | 2 |\n",
    "+    1    2            |___|___|\n",
    "```\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "5f0c8ef3-7cbd-4c61-9a46-02aa632d6f60",
   "metadata": {},
   "outputs": [],
   "source": [
    ";;; Traduzioni in italiano di alcuni simboli del linguaggio Scheme\n",
    "\n",
    "; Predicato lista? (traduzione di list?)\n",
    "(define lista?\n",
    "  (lambda (l)\n",
    "         (list? l)))\n",
    "\n",
    "; Predicato lista-vuota? (traduzione di null?)\n",
    "(define lista-vuota?\n",
    "  (lambda (l)\n",
    "    (cond\n",
    "      [(not (list? l)) (display \"'lista-vuota' è definita solo per liste\")]\n",
    "      [else (null? l)])))\n",
    "\n",
    "; Costante lista-vuota\n",
    "(define lista-vuota '())\n",
    "\n",
    "; Primo elemento di una lista\n",
    "(define primo\n",
    "  (lambda (l)\n",
    "    (cond\n",
    "      [(not (lista? l)) (display \"'primo' è definito solo per le liste\")]\n",
    "      [(null? l) (display \"'primo' non è definito per la lista vuota\")]\n",
    "       [else (car l)])))\n",
    "\n",
    "; Il resto di una lista\n",
    "(define resto\n",
    "  (lambda (l)\n",
    "    (cond\n",
    "      ;; [(not (list? l)) (display \"'resto' è definito solo per le liste\")]\n",
    "      [(null? l) (display \"'resto' non è definito per la lista vuota\")]\n",
    "      [else (cdr l)])))\n",
    "\n",
    "; Anteponi\n",
    "(define anteponi\n",
    "  (lambda (a l)\n",
    "    (cond\n",
    "      [(not (list? l)) (display \"'anteponi' è definito solo se il secondo argomento è una lista\")]\n",
    "      [else (cons a l)])))\n",
    "\n",
    "; Predicato uguale?\n",
    "(define uguale?\n",
    "  (lambda (a1 a2)\n",
    "    (cond\n",
    "      [(or (not (and (atomo? a1) (atomo? a2))) (number? a1) (number? a2)) (display \"'uguale' è definito solo per atomi non numerici\")]\n",
    "      [else (eq? a1 a2)])))\n",
    "\n",
    "; Lista\n",
    "(define lista list)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "73c2cb8d-e34f-4a76-a0c6-f70b7a40bba1",
   "metadata": {},
   "source": [
    "## Prefazione\n",
    "\n",
    "Qualche (ri-)definizione, in italiano, per fissare qualche termine.\n",
    "\n",
    "L'obiettivo è comprendere come scrivere espressioni simboliche costituite da atomi e liste.\n",
    "Negli esempi in aula viene visualizzata la struttura ad albero delle espressioni (albero sintattico) e il processo di valutazione/riscrittura."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "2822cd91-350b-4a47-82f9-6ba1b25cb8a4",
   "metadata": {},
   "outputs": [],
   "source": [
    ";;; Preface\n",
    "\n",
    "; Predicato atomo? (traduzione di atom?, p. xii)\n",
    "(define atomo?\n",
    "  (lambda\n",
    "      (x)\n",
    "    (and (not (pair? x)) (not (null? x)))))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f68140cd-4473-467c-949e-c3fea76f9efd",
   "metadata": {},
   "source": [
    "## 1. Giocattoli"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "a3c7b329-af14-4acb-b5a5-f63b265d24ab",
   "metadata": {},
   "outputs": [],
   "source": [
    ";;; 1. Toys\n",
    "\n",
    "; Provare gli esempi del libro e confrontare e confrontare le risposte"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dfc159fd-ce67-4f38-8dbf-e2fa5117f109",
   "metadata": {},
   "source": [
    "## 2. Fallo ancora, ancora ed ancora..."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "f9805468-7f69-4152-9c25-ef602669afb5",
   "metadata": {},
   "outputs": [],
   "source": [
    ";;; 2. Do It, Do It Again, and Again, and Again...\n",
    "\n",
    "; Predicato lista-di-atomi? p. 16\n",
    "(define lista-di-atomi?\n",
    "  (lambda (l)\n",
    "    (cond\n",
    "      [(lista-vuota? l) #t]\n",
    "      [(atomo? (primo l)) (lista-di-atomi? (resto l))]\n",
    "      [else #f])))\n",
    "\n",
    "; Predicato membro. p. 22\n",
    "(define membro?\n",
    "  (lambda (a lda)\n",
    "    (cond\n",
    "      [(lista-vuota? lda) #f]\n",
    "      [else (or (uguale? a (primo lda))\n",
    "                (membro? a (resto lda)))])))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "31024891-012f-4c1c-8d5c-19e34024fc13",
   "metadata": {},
   "source": [
    "## 3. Anteporre atomi a liste"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "68219c9f-c942-43b6-bc6a-f5c4afe588f1",
   "metadata": {},
   "outputs": [],
   "source": [
    ";;; 3. Cons the Magnificent\n",
    "\n",
    "; Rimuovi un membro. p. 34\n",
    "(define rimuovi-un-membro\n",
    "  (lambda (a lda)\n",
    "    (cond\n",
    "      [(lista-vuota? lda) lista-vuota]\n",
    "      [(uguale? a (primo lda)) (resto lda)]\n",
    "      [else (anteponi (primo lda) (rimuovi-un-membro a (resto lda)))])))\n",
    "\n",
    "; Lista dei primi elementi delle sottoliste. p. 44\n",
    "(define primi\n",
    "  (lambda (l)\n",
    "    (cond\n",
    "     [(lista-vuota? l) lista-vuota]\n",
    "     [else (anteponi (primo (primo l)) (primi (resto l)))])))\n",
    "\n",
    "; Inserisce il nuovo elmento a destra del vecchio. p. 49\n",
    "(define inserisci-a-destra\n",
    "  (lambda (nuovo vecchio lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(uguale? (primo lista-di-atomi) vecchio) (anteponi vecchio (anteponi nuovo (resto lista-di-atomi)))]\n",
    "     [else (anteponi (primo lista-di-atomi) (inserisci-a-destra nuovo vecchio (resto lista-di-atomi)))])))\n",
    "\n",
    "; Inserisce il nuovo elmento a sinitra del vecchio. p. 50\n",
    "(define inserisci-a-sinistra\n",
    "  (lambda (nuovo vecchio lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(uguale? (primo lista-di-atomi) vecchio) (anteponi nuovo lista-di-atomi)]\n",
    "     [else (anteponi (primo lista-di-atomi) (inserisci-a-sinistra nuovo vecchio (resto lista-di-atomi)))])))\n",
    "\n",
    "; Sostituisci. p. 51\n",
    "(define sostituisci\n",
    "  (lambda (nuovo vecchio lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(uguale? (primo lista-di-atomi) vecchio) (anteponi nuovo (resto lista-di-atomi))]\n",
    "     [else (anteponi (primo lista-di-atomi) (sostituisci nuovo vecchio (resto lista-di-atomi)))])))\n",
    "\n",
    "; sostituisci-il-primo-dei-due. p. 52\n",
    "(define sostituisci-il-primo-dei-due\n",
    "  (lambda (nuovo vecchio-1 vecchio-2 lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(or (uguale? (primo lista-di-atomi) vecchio-1)\n",
    "          (uguale? (primo lista-di-atomi) vecchio-2)) (anteponi nuovo (resto lista-di-atomi))]\n",
    "     [else (anteponi (primo lista-di-atomi) (sostituisci-il-primo-dei-due nuovo vecchio-1 vecchio-2 (resto lista-di-atomi)))])))\n",
    "\n",
    "; Rimuovi tutti i membri. p. 54\n",
    "(define rimuovi-tutti-i-membri\n",
    "  (lambda (a lda)\n",
    "    (cond\n",
    "      [(lista-vuota? lda) lista-vuota]\n",
    "      [(uguale? a (primo lda)) (rimuovi-tutti-i-membri a (resto lda))]\n",
    "      [else (anteponi (primo lda) (rimuovi-tutti-i-membri a (resto lda)))])))\n",
    "\n",
    "; Inserisci tutti a destra. p. 56\n",
    "(define inserisci-tutti-a-destra\n",
    "  (lambda (nuovo vecchio lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(uguale? (primo lista-di-atomi) vecchio)\n",
    "          (anteponi vecchio \n",
    "                    (anteponi nuovo\n",
    "                              (inserisci-tutti-a-destra nuovo vecchio \n",
    "                                                        (resto lista-di-atomi))))]\n",
    "     [else (anteponi (primo lista-di-atomi) (inserisci-tutti-a-destra nuovo vecchio (resto lista-di-atomi)))])))\n",
    "\n",
    "; Inserisci tutti a sinistra. p. 56\n",
    "(define inserisci-tutti-a-sinistra\n",
    "  (lambda (nuovo vecchio lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(uguale? (primo lista-di-atomi) vecchio)\n",
    "          (anteponi nuovo \n",
    "                    (anteponi vecchio\n",
    "                              (inserisci-tutti-a-sinistra nuovo vecchio \n",
    "                                                        (resto lista-di-atomi))))]\n",
    "     [else (anteponi (primo lista-di-atomi) (inserisci-tutti-a-sinistra nuovo vecchio (resto lista-di-atomi)))])))\n",
    "\n",
    "; Sostituisci tutti. p. 57\n",
    "(define sostituisci-tutti\n",
    "  (lambda (nuovo vecchio lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) lista-vuota]\n",
    "     [(uguale? (primo lista-di-atomi) vecchio) (anteponi nuovo (sostituisci-tutti nuovo vecchio (resto lista-di-atomi)))]\n",
    "     [else (anteponi (primo lista-di-atomi) (sostituisci-tutti nuovo vecchio (resto lista-di-atomi)))]))) \n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b1badc69-ccdd-4054-9001-4d54142fcfc9",
   "metadata": {},
   "source": [
    "## 4. Giochi di numeri"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "f3849456-c44e-4506-a70c-1ac44331cb4b",
   "metadata": {},
   "outputs": [],
   "source": [
    ";\n",
    "; Definizione delle primitive\n",
    ";\n",
    "\n",
    "; Successore di un numero naturale n. p. 59 add1\n",
    "(define s\n",
    "  (lambda (n)\n",
    "    (+ n 1)))\n",
    "\n",
    "; Predecessore di un numero naturale n. p. 59 sub1\n",
    "(define p\n",
    "  (lambda (n)\n",
    "    (- n 1)))\n",
    "\n",
    "; Zero? è già definito dal linguaggio Scheme. \n",
    "\n",
    "(define numero?\n",
    "  (lambda (n)\n",
    "          (number? n)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 33,
   "id": "d90c580e-cfda-4d2f-a178-0c608a6f435f",
   "metadata": {},
   "outputs": [],
   "source": [
    ";\n",
    "; Definizione delle operazioni\n",
    ";\n",
    "\n",
    "; o+, p. 60\n",
    "\n",
    "(define più\n",
    "  (lambda (n m)\n",
    "    (cond\n",
    "     [(zero? m) n]\n",
    "     [else (s (più n (p m)))])))\n",
    "\n",
    "; o-. p. 61\n",
    "(define meno\n",
    "  (lambda (n m)\n",
    "    (cond\n",
    "     [(zero? m) n]\n",
    "     [else (meno (p n) (p m))])))\n",
    "\n",
    "\n",
    "; tup? p. 61\n",
    "(define n-upla?\n",
    "  (lambda (n-upla)\n",
    "    (cond\n",
    "     [(lista-vuota? n-upla) #t]\n",
    "     [else (and (numero? (primo n-upla)) (n-upla? (resto n-upla)))])))\n",
    "\n",
    "; addtup. p. 62\n",
    "(define somma\n",
    "  (lambda (n-upla)\n",
    "    (cond\n",
    "     [(lista-vuota? n-upla) 0]\n",
    "     [else (+ (primo n-upla) (somma (resto n-upla)))])))\n",
    "\n",
    "; p. 65\n",
    "(define per\n",
    "  (lambda (n m)\n",
    "    (cond\n",
    "     [(zero? m) 0]\n",
    "     [else (+ n (per n (p m)))])))\n",
    "\n",
    "; tup+. p. 67\n",
    "(define addizione-n-uple\n",
    "  (lambda (v1 v2)\n",
    "    (cond\n",
    "     [(lista-vuota? v1) v2]\n",
    "     [(lista-vuota? v2) v1]\n",
    "     [else (anteponi (+ (primo v1)\n",
    "                        (primo v2))\n",
    "                     (addizione-n-uple (resto v1)\n",
    "                                       (resto v2)))])))\n",
    "\n",
    "; >. p. 71\n",
    "(define maggiore?\n",
    "    (lambda (n m)\n",
    "      (cond\n",
    "        [(zero? n) #f]\n",
    "        [(zero? m) #t]\n",
    "        [else (maggiore? (p n) (p m))])))\n",
    "\n",
    "; <. p. 73\n",
    "(define minore?\n",
    "  (lambda (n m)\n",
    "    (cond\n",
    "      [(zero? m) #f]\n",
    "      [(zero? n) #t]\n",
    "      [else (minore? (p n) (p m))])))\n",
    "\n",
    "; =. p. 74\n",
    "(define stesso-numero?\n",
    "  (lambda (n m)\n",
    "    (cond\n",
    "      [(minore? n m) #f]\n",
    "      [(maggiore? n m) #f]\n",
    "      [else #t])))\n",
    "\n",
    "; p. 74\n",
    "(define potenza\n",
    "  (lambda (base esponente)\n",
    "    (cond\n",
    "     [(zero? esponente) 1]\n",
    "     [else (* base (potenza base (p esponente)))])))\n",
    "\n",
    "; Dai un nome a questa procedura\n",
    "; (define ???\n",
    ";   (lambda ( n m )\n",
    ";     (cond\n",
    ";     [( < n m ) 0]\n",
    ";     [else (s (??? (- n m) m ))])))\n",
    "\n",
    "; p. 75\n",
    "(define diviso\n",
    "  (lambda ( n m )\n",
    "    (cond\n",
    "     [( < n m ) 0]\n",
    "     [else (s (diviso (- n m) m ))])))\n",
    "\n",
    "; length. p. 76\n",
    "(define lunghezza\n",
    "  (lambda (lista)\n",
    "    (cond\n",
    "         [(lista-vuota? lista) 0]\n",
    "         [else (+ 1 (lunghezza (resto lista)))])))\n",
    "\n",
    "; pick. p. 76\n",
    "(define seleziona\n",
    "  (lambda (posizione lista)\n",
    "    (cond\n",
    "     [(zero? (p posizione)) (primo lista)]\n",
    "     [else (seleziona (p posizione) (resto lista))])))\n",
    "\n",
    "; rempick. p. 76\n",
    "(define rimuovi-selezionato\n",
    "  (lambda (posizione lista)\n",
    "    (cond\n",
    "     [(lista-vuota? lista) lista-vuota]\n",
    "     [(zero? (p posizione)) (resto lista)]\n",
    "     [else (anteponi (primo lista) (rimuovi-selezionato (p posizione) (resto lista)))])))\n",
    "\n",
    "; no-nums. p. 77\n",
    "(define rimuovi-numeri\n",
    "  (lambda (lista)\n",
    "    (cond\n",
    "     [(lista-vuota? lista) lista-vuota]\n",
    "     [(numero? (primo lista)) (rimuovi-numeri (resto lista))]\n",
    "     [else (anteponi (primo lista) (rimuovi-numeri (resto lista)))])))\n",
    "\n",
    "; all-nums. p. 78\n",
    "(define solo-numeri\n",
    "  (lambda (lista)\n",
    "    (cond\n",
    "     [(lista-vuota? lista) lista-vuota]\n",
    "     [(numero? (primo lista)) (anteponi (primo lista) (solo-numeri (resto lista)))]\n",
    "     [else (solo-numeri (resto lista))])))\n",
    "\n",
    "; eqan? p. 78\n",
    "(define stesso-atomo?\n",
    "  (lambda (a1 a2)\n",
    "    (cond\n",
    "     [(and (numero? a1) (numero? a2)) (= a1 a2)]\n",
    "     [(or (numero? a1) (numero? a2)) #f]\n",
    "     [else (uguale? a1 a2)])))\n",
    "\n",
    "; occur. p. 78\n",
    "(define occorrenze\n",
    "  (lambda (a lista-di-atomi)\n",
    "    (cond\n",
    "     [(lista-vuota? lista-di-atomi) 0]\n",
    "     [(stesso-atomo? (primo lista-di-atomi) a)\n",
    "          (s (occorrenze a (resto lista-di-atomi)))]\n",
    "     [else (occorrenze a (resto lista-di-atomi))])))\n",
    "\n",
    "; one? p. 79\n",
    "(define uno?\n",
    "  (lambda (n)\n",
    "    (= n 1)))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Calysto Scheme 3",
   "language": "scheme",
   "name": "calysto_scheme"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "scheme"
   },
   "mimetype": "text/x-scheme",
   "name": "scheme",
   "pygments_lexer": "scheme"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
